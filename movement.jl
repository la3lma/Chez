
###
###  Representing movement
###


##
## Representing moves
##


# Representing moves
struct Move
    start:: Coord
    destination:: Coord
    capture:: Bool
    startPiece:: ChessPiece
    destinationPiece:: ChessPiece
    # XXX Must include a field stating if it's a chess-producing move or not
end

# Take a look at http://en.wikipedia.org/wiki/Chess_notation, Print moves
# in an orderly proficient algebraic or long algebraic manner.

# The two implementations below are not really conformant

# Standard Algebraic Notation (SAN)
function move_as_san(m::Move)
    capture = m.capture ? "x" : ""
    return @sprintf("%s%s%s%s", m.startPiece.printrep, m.start, capture, m.destination)
end

# Figurine Algebraic Notation (FAN)
function move_as_san(m::Move)
    capture = m.capture ? "x" : ""
    return @sprintf("%s%s%s%s", m.startPiece.unicode, m.start, capture, m.destination)
end


show(io::IO, m::Move) = show(io, move_as_san(m))


function ==(m1::Move, m2::Move)
     return (m1.start == m2.start) &&
            (m1.destination == m2.destination) &&
	    (m1.capture == m2.capture) &&
	    (m1.startPiece == m2.startPiece) &&
	    (m1.destinationPiece == m2.destinationPiece)
end


##
##  Finding all valid moves, generic code
##

function get_all_coords()
    result = Coord[]
     for y = 1:8, x = 1:8
        c = Coord(x, y)
        push!(result, c)
     end
     return result
end

coords = get_all_coords()

@test 64 == length(coords)

# Get coordinates for all the pieces of a particular
# color, these coordinates will then be used to generate
# all possible moves for that color.
function get_coords_for_pieces(color::Color, board::ChessBoard)
   return filter(c -> (getPieceAt(board, c).color == color), coords)
end


#  Given an iterable of moves, will filter out anything that is not
#  a valid move, either because it's a nothing object, or because
#  the endpoint coordinate is out of bounds.
function valid_moves(moves)
    move_is_valid(::Nothing) = false
    move_is_valid(m::Move)   = isValidCoord(m.destination)

    return  filter(move_is_valid, moves)
end


# Starting with a piece at the start coordinate, moving that
# piece using the jump offset generates s candidate move.
# If the target is a valid coordinate, and the move isn't
# attempting to capture one of the moving player's own pieces, then
# a move instance is generated.  The additional parameter
# "requireCapture" will, if set, force the generated move to
# be a capture.  That latter functionality is only used
# by pawns that can only move  forward and sideways when
# capturing.
function move_from_jump(board::ChessBoard, start::Coord, jump::Coord, requireCapture::Bool = false)
    destination = start + jump

    if (!isValidCoord(destination))
        return nothing
    end

    destinationPiece = getPieceAt(board, destination)
    startPiece = getPieceAt(board, start)

    isLegalMove = destinationPiece.color != startPiece.color
    isCapture = isLegalMove && (destinationPiece.color != transparent)

    if (!isLegalMove || (requireCapture && !isCapture))
       return nothing
    else
       return Move(start, destination, isCapture, startPiece, destinationPiece)
    end
end

# Test movement of a single pawn
@test Move(a2, a3, false, wp, bs) == move_from_jump(startingBoard, a2, Coord(0,1))


moves_from_jumps(board::ChessBoard, start::Coord, jumps, requireCaptures::Bool) =
       [ move_from_jump(board, start, j, requireCaptures) for j in jumps ] |> valid_moves


@test [ Move(a2, a3, false, wp, bs)]   == moves_from_jumps(startingBoard, a2,[Coord(0,1)], false)

# XXX Placeholder.  When we are able to generate all moves for all types, this
#     "catch-all" movement generator should either be removed, or throw some kind of
#     exception.
function get_moves_for_piece(piece::PieceType, color::Color,  board::ChessBoard, coord::Coord)
  []
end

# Same as above.  Remove when complete movement generation has been implemented.
function get_moves_for_piece(piece::Blank, board::ChessBoard, coord::Coord)
  []  # Arguably, this should throw an exception instead, or return nothing.
end



# Rooks, kings, bishops are all made up by
# rays and filtering.  A "ray" is a sequence of move-candidates generated by
# a starting position, and then stepping in some direction using the direction.
# The generation stops if the destination has travelled off the
# board, or a capture candidate has been generated.

function get_moves_from_ray(
	 direction::Coord,
	 color::Color,
	 board::ChessBoard,
	 start::Coord,
	 oneStepOnly::Bool)

    destination = start + direction
    result = []
    capture=false
    startPiece = getPieceAt(board, start)

    while (isValidCoord(destination) && !capture)
        destinationPiece = getPieceAt(board, destination)

        # Is the piece at the destination the same as the
        # piece being move, if so break off. No more moves
        # to generate.
	if (destinationPiece.color == startPiece.color)
          break
        end

        # Otherwise, if we're not looking at a blank space
        # as the target, we're capturing something
	capture = (bs != destinationPiece)

        # Generate the move and putadd it to the result
        move = Move(start, destination, capture, startPiece, destinationPiece)
        push!(result, move)

        # If we were only supposed to go one step (as for pawns)
        # then break now, otherwise continue.
	if (oneStepOnly)
           break
        else
	   destination +=  direction            
        end
    end
    return result
end

get_moves_from_rays(directions::Array{Coord, 1}, color::Color, board::ChessBoard, coord::Coord, oneStepOnly::Bool = false) = 
    [ get_moves_from_ray(gen, color, board, coord, oneStepOnly) for gen in directions]


bishop_ray_directions = [Coord(1,1), Coord(-1,-1), Coord(-1, 1), Coord(1, -1)]

function get_moves_for_piece(piece::Bishop, color::Color,  board::ChessBoard, coord::Coord)
	 get_moves_from_rays(bishop_ray_directions, color, board, coord)
end

rook_ray_directions = [Coord(0,1), Coord(0,-1), Coord(1, 0), Coord(-1, 0)]

function get_moves_for_piece(piece::Rook, color::Color,  board::ChessBoard, coord::Coord)
	 get_moves_from_rays(rook_ray_directions, color, board, coord)
end


flatten_moves(x) = x |> Iterators.flatten |> collect

function get_royal_moves(color::Color, board::ChessBoard, coord::Coord,
    oneStepOnly::Bool = false) return flatten_moves(
    [get_moves_from_rays(bishop_ray_directions, color, board, coord,
    oneStepOnly), get_moves_from_rays(rook_ray_directions, color, board,
    coord, oneStepOnly)]) end

is_legal_king_move(move::Move, board::ChessBoard) = false

get_moves_for_piece(piece::Queen, color::Color,  board::ChessBoard, coord::Coord) =  get_royal_moves(color, board, coord, false)
get_moves_for_piece(piece::King, color::Color,  board::ChessBoard, coord::Coord) =
      flatten_moves(get_royal_moves(color, board, coord, true)) |>   moves -> filter(m->is_legal_king_move(m, board), moves)


knightJumps = [Coord(-2, 1), Coord(2, 1),   Coord(1, 2),    Coord(-1, 2),
 	       Coord(2, -1), Coord(-2, -1), Coord(-1, -2),  Coord(1, -2)]

function get_moves_for_piece(piece::Knight, color::Color, board::ChessBoard, coord::Coord)
    moves_from_jumps(board, coord, knightJumps, false)
end


# this test shouldn't depend on the order of items in the list, it should treat
# this as a test for collection equality
@test [ Move(b1, c3, false, wk, bs), Move(b1, a3, false, wk, bs)] == get_moves_for_piece(wk.piecetype, white,  startingBoard, b1)



function get_moves_for_piece(piece::Pawn, color::Color,  board::ChessBoard, coord::Coord)

  pawn_start_line(color::Color) =  (color == black) ? 7 : 2
  finish_line(color::Color)     =  (color == black) ? 1 : 8

    
  # First we establish a jump direction that is color dependent
  # (for pawns only)
  speed = (color == white) ? 1 : -1

  # Then we establish a single non-capturing movement ray
  if  (coord.y == pawn_start_line(color))
     ncray = [Coord(0, speed), 2 * Coord(0, speed)]
  else
     ncray = [Coord(0, speed)]
  end

  # And a set of possibly capturing jumps
  captureJumps = [Coord(1,speed), Coord(-1, speed)]


  # This is  such a kludge!
  moves = flatten_moves(vcat([moves_from_jumps(board, coord, ncray, false),
    		moves_from_jumps(board, coord, captureJumps, true)]))

  moves = filter(move_is_valid, moves)

  # Finally we do the pawn-specific tranformation
  # if we find ourself ending up on the finishing line
  for move in moves
      if (move.destination.y == finish_line(color))
	 move.piece = queenOfColor(color)
      end
  end
  return moves
end

@test 2 == length(get_moves_for_piece(pawn,   white, startingBoard, a2))
@test 2 == length(get_moves_for_piece(knight, white, startingBoard, b1))


# From a chessboard, extract all the possible moves for all
# the pieces for a particular color on the board.
# Return an array (a set) of Move instances
get_moves(color::Color, board::ChessBoard) =
    filter(m -> m isa Move, flatten_moves([get_moves_for_piece(getPieceAt(startingBoard, c).piecetype, color, startingBoard, c)
                               for c in get_coords_for_pieces(color,startingBoard)]))



# All the opening moves for pawns and horses
@test 20 == length(get_moves(white, startingBoard))
@test 20 == length(get_moves(black, startingBoard))
